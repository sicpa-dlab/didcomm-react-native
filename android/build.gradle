def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    // The Android Gradle plugin is only required when opening the android folder stand-alone.
    // This avoids unnecessary downloads and potential conflicts when the library is included as a
    // module dependency in an application project.
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
        }
        dependencies {
            classpath("com.android.tools.build:gradle:4.2.2")
            classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.21")
        }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    compileSdkVersion safeExtGet("compileSdkVersion", 31)
    buildToolsVersion safeExtGet("buildToolsVersion", "30.0.2")
    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", 24)
        targetSdkVersion safeExtGet("targetSdkVersion", 31)
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    maven {
        // Android JSC is installed from npm
        url("$rootDir/../node_modules/jsc-android/dist")
    }
    google()
    maven { url "https://www.jitpack.io" }
}

dependencies {
    implementation "com.facebook.react:react-native:+" // From node_modules
    implementation("org.didcommx:didcomm:0.3.0")
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1"
    implementation "com.google.code.gson:gson:2.9.0"
    implementation "org.json:json:20220320"
}